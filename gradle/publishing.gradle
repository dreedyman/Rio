/*
 * Copyright to the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

project.apply plugin: 'com.jfrog.bintray'
apply from: "${rootDir}/gradle/poms.gradle"

if (project.name != "distribution" && project.name != "rio-int-tests" && !project.path.contains("example")) {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc.failOnError = false
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', "$project.description")
                    root.appendNode('name', "${project.name}")
                    root.appendNode('url', 'https://github.com/dreedyman/Rio')
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

bintray {
    dryRun = true
    user = project.property('bintray.user')
    key = project.property('bintray.key')
    publications = project.name == "distribution" ? ['distribution'] : ['mavenJava']

    pkg {
        repo = 'Rio'
        name = project.archivesBaseName
        licenses = ['Apache-2.0']
        publish = true
        desc = project.description
        vcsUrl = 'https://github.com/dreedyman/Rio'
        version {
            name = project.version
            gpg {
                sign = true
            }
        }
    }
}
